allprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'
}

idea {
    project {
        jdkName = JavaVersion.VERSION_1_8
        languageLevel = JavaVersion.VERSION_1_8

        vcs = "Git"
    }

    workspace.iws.withXml { provider ->
        def junitDefaults = provider.node.component.find { it.@name == 'RunManager' }.configuration.find {
            it.@type == 'JUnit'
        }
        junitDefaults.option.find { it.@name == 'WORKING_DIRECTORY' }.@value = '$MODULE_DIR$'
    }
}

subprojects {
    group = 'com.github.dreamhead'
    version = '1.0.0-SNAPSHOT'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked"
        options.compilerArgs << "-Xlint:deprecation"
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    repositories {
        jcenter()
    }

    project.dependencies {
        implementation("com.google.guava:guava:$guavaVersion")
        testImplementation("org.assertj:assertj-core:$assertJVersion")
    }

    apply from: "$rootDir/gradle/config/scripts/style.gradle"
    apply from: "$rootDir/gradle/config/scripts/coverage.gradle"

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = project.group
                artifactId = project.name
                version = project.version

                from components.java

                pom {
                    name = project.name
                    description = 'Object bot is a library for setting up Java objects as test data.'
                    url = 'https://github.com/dreamhead/object-bot'
                    licenses {
                        license {
                            name = 'MIT Licence'
                            url = 'https://raw.githubusercontent.com/dreamhead/object-bot/master/MIT-LICENSE.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'dreamhead'
                            name = 'Zheng Ye'
                            email = 'dreamhead.cn@gmail.com'
                        }
                    }
                    scm {
                        connection = 'https://github.com/dreamhead/object-bot.git'
                        developerConnection = 'https://github.com/dreamhead/object-bot.git'
                        url = 'https://github.com/dreamhead/object-bot'
                    }
                }
            }
        }

        repositories {
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

    project.jar {
        manifest {
            attributes 'Package': "com.github.dreamhead",
                    'Implementation-Title': "${project.name}",
                    'Implementation-Version': "${archiveVersion.get()}",
                    'Implementation-Vendor': 'Zheng Ye',
                    'Built-By': 'Zheng Ye',
                    'Built-Date': new Date().getDateTimeString(),
                    'Built-With': "gradle-${project.getGradle().getGradleVersion()}, groovy-${GroovySystem.getVersion()}",
                    'Created-By': 'Java ' + System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')'
        }
    }
}

configure(subprojects - project(':bot-core')) {
    project.dependencies {
        api(project(':bot-core'))
    }
}